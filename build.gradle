buildscript {
    ext.kotlin_version = '1.3.50'
    ext.jline_version = '3.12.1'
    ext.antlr_version = '4.7.2'
    ext.aws_sdk_version = '1.11.648'
    ext.spek_version = '1.1.5'
    ext.hamcrest_version = '1.3'
    ext.jackson_version = '2.10.0'
    ext.kafka_version = '1.1.0'
    ext.hll_version = '1.1'

    repositories {
        mavenCentral()
        jcenter()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath 'com.github.jengelman.gradle.plugins:shadow:4.0.3'
        classpath 'org.junit.platform:junit-platform-gradle-plugin:1.0.0'
        classpath 'net.sf.proguard:proguard-gradle:6.0.3'
    }
}

import proguard.gradle.ProGuardTask

group 'jsonsql'
version '1.0-SNAPSHOT'

apply plugin: 'java'
apply plugin: 'kotlin'
apply plugin: 'antlr'
apply plugin: 'com.github.johnrengelman.shadow'
apply plugin: 'org.junit.platform.gradle.plugin'

sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

// stops us bundling full antlr compiler in shadowjar
configurations {
    compile {
        extendsFrom = extendsFrom.findAll { it != configurations.antlr }
    }
}

junitPlatform {
    filters {
        engines {
            include 'spek'
        }
    }
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    compile "com.fasterxml.jackson.dataformat:jackson-dataformat-csv:$jackson_version"
    compile "org.jline:jline-reader:$jline_version"
    compile "org.jline:jline-terminal-jna:$jline_version"
    antlr "org.antlr:antlr4:$antlr_version"
    compile "org.antlr:antlr4-runtime:$antlr_version"
    compile "com.amazonaws:aws-java-sdk-s3:$aws_sdk_version"
    compile "com.amazonaws:aws-java-sdk-sts:$aws_sdk_version"
    compile "org.apache.kafka:kafka-clients:$kafka_version"
    compile ("com.github.prasanthj:hyperloglog:$hll_version") {
        exclude group: "commons-cli"
    }

    testCompile ("org.jetbrains.spek:spek-api:$spek_version") {
        exclude group: 'org.jetbrains.kotlin'
    }
    testRuntime ("org.jetbrains.spek:spek-junit-platform-engine:$spek_version") {
        exclude group: 'org.junit.platform'
        exclude group: 'org.jetbrains.kotlin'
    }
    testCompile "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
    testCompile "org.hamcrest:hamcrest-all:$hamcrest_version"
    testCompile 'org.junit.platform:junit-platform-runner:1.0.0'
}

compileKotlin {
    dependsOn generateGrammarSource
}

shadowJar {
    classifier = null
    version = null
}

jar {
    manifest {
        attributes 'Main-Class': 'jsonsql.shell.ShellKt'
    }
}

task proguard(type: ProGuardTask, dependsOn: 'shadowJar') {
    injars "$buildDir/libs/jsonsql.jar"
    outjars "$buildDir/libs/min/jsonsql.jar"

    if (System.getProperty('java.version').startsWith('1.')) {
        // Before Java 9, the runtime classes were packaged in a single jar file.
        libraryjars "${System.getProperty('java.home')}/lib/rt.jar"
    } else {
        // As of Java 9, the runtime classes are packaged in modular jmod files.
        libraryjars "${System.getProperty('java.home')}/jmods/java.base.jmod", jarfilter: '!**.jar', filter: '!module-info.class'
    }

    dontobfuscate
    dontwarn "com.**"
    dontwarn "software.**"
    dontwarn "org.**"
    dontwarn "net.jpountz.util.UnsafeUtils"

    keep 'public class jsonsql.shell.ShellKt { public static void main(java.lang.String[]); }'

    keepclassmembers """class * extends java.lang.Enum {
        <fields>;
        public static **[] values();
        public static ** valueOf(java.lang.String);
    }"""

    optimizations "!code/allocation/variable"
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}