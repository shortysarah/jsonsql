buildscript {
    // Deps
    ext.kotlin_version = '1.3.61'
    ext.jline_version = '3.13.3'
    ext.antlr_version = '4.8-1'
    ext.aws_sdk_version = '1.11.711'
    ext.jackson_version = '2.10.2'
    ext.kafka_version = '2.4.0'
    ext.graalvm_version = '19.2.1'
    ext.slf4j_version = '1.7.28' // Should be kept in sync with the version pulled in by kafka

    // Test Deps
    ext.junit_platform_runner_version = '1.6.0'
    ext.spek_version = '2.0.9'
    ext.hamcrest_version = '1.3'
    ext.kafka_test_version = '3.2.1'

    // Plugins
    ext.shadow_version = '5.2.0'
    ext.versions_plugin_version = '0.27.0'

    repositories {
        mavenCentral()
        jcenter()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "com.github.jengelman.gradle.plugins:shadow:$shadow_version"
        classpath "com.github.ben-manes:gradle-versions-plugin:$versions_plugin_version"
    }
}

group 'jsonsql'
version '1.0-SNAPSHOT'

apply plugin: 'java'
apply plugin: 'kotlin'
apply plugin: 'antlr'
apply plugin: 'com.github.johnrengelman.shadow'
apply plugin: 'com.github.ben-manes.versions'

sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

// stops us bundling full antlr compiler in shadowjar
configurations {
    compile {
        extendsFrom = extendsFrom.findAll { it != configurations.antlr }
    }
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    implementation "com.fasterxml.jackson.dataformat:jackson-dataformat-csv:$jackson_version"
    implementation "org.jline:jline-reader:$jline_version"
    implementation "org.jline:jline-terminal-jna:$jline_version"
    antlr "org.antlr:antlr4:$antlr_version"
    implementation "org.antlr:antlr4-runtime:$antlr_version"
    implementation "com.amazonaws:aws-java-sdk-s3:$aws_sdk_version"
    implementation "com.amazonaws:aws-java-sdk-sts:$aws_sdk_version"
    implementation "org.apache.kafka:kafka-clients:$kafka_version"
    implementation "org.slf4j:slf4j-nop:$slf4j_version"
    compileOnly "com.oracle.substratevm:svm:$graalvm_version"

    testImplementation "org.junit.platform:junit-platform-runner:$junit_platform_runner_version"
    testImplementation "org.spekframework.spek2:spek-dsl-jvm:$spek_version"
    testRuntimeOnly "org.spekframework.spek2:spek-runner-junit5:$spek_version"
    testRuntimeOnly "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
    testImplementation "org.hamcrest:hamcrest-all:$hamcrest_version"
    testImplementation "com.salesforce.kafka.test:kafka-junit5:$kafka_test_version"
      // Force newer version of curator-test as used by kafka-junit5 for now to get around java 9 module warnings
      testRuntimeOnly "org.apache.curator:curator-test:4.2.0"

    testRuntimeOnly "org.apache.kafka:kafka_2.13:$kafka_version"


}

compileKotlin {
    dependsOn generateGrammarSource
    kotlinOptions.jvmTarget = "1.8"
}

compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

test {
    useJUnitPlatform {
        includeEngines 'spek2'
    }
    testLogging {
        events "failed"
        exceptionFormat "full"
    }
}

// This is used to test against the binary produced by Graal as some things may not work in the native
// image so it's good to check
task externalIntegrationTest(type: Test) {
    systemProperty "test.external.binary", "true"

    useJUnitPlatform {
        includeEngines 'spek2'
    }
    testLogging {
        events "failed"
        exceptionFormat "full"
        showStandardStreams = true
    }
    outputs.upToDateWhen { false }
}


jar {
    manifest {
        attributes 'Main-Class': 'jsonsql.shell.ShellKt'
    }
}

shadowJar {
    archiveClassifier = null
    archiveVersion = ''
}
