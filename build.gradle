buildscript {
    ext.kotlin_version = '1.3.61'
    ext.jline_version = '3.13.3'
    ext.antlr_version = '4.8-1'
    ext.aws_sdk_version = '1.11.711'
    ext.spek_version = '2.0.9'
    ext.hamcrest_version = '1.3'
    ext.jackson_version = '2.10.2'
    ext.kafka_version = '2.4.0'
    ext.graalvm_version = '19.2.1'
    ext.arrow_version = '0.15.1'

    ext.junit_platform_runner_version = '1.6.0'

    ext.shadow_version = '5.2.0'

    repositories {
        mavenCentral()
        jcenter()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "com.github.jengelman.gradle.plugins:shadow:$shadow_version"
        classpath "com.github.ben-manes:gradle-versions-plugin:0.27.0"
    }
}

group 'jsonsql'
version '1.0-SNAPSHOT'

apply plugin: 'java'
apply plugin: 'kotlin'
apply plugin: 'antlr'
apply plugin: 'com.github.johnrengelman.shadow'
apply plugin: 'com.github.ben-manes.versions'

sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

// stops us bundling full antlr compiler in shadowjar
configurations {
    compile {
        extendsFrom = extendsFrom.findAll { it != configurations.antlr }
    }
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    implementation "com.fasterxml.jackson.dataformat:jackson-dataformat-csv:$jackson_version"
    implementation "org.jline:jline-reader:$jline_version"
    implementation "org.jline:jline-terminal-jna:$jline_version"
    antlr "org.antlr:antlr4:$antlr_version"
    implementation "org.antlr:antlr4-runtime:$antlr_version"
    implementation "com.amazonaws:aws-java-sdk-s3:$aws_sdk_version"
    implementation "com.amazonaws:aws-java-sdk-sts:$aws_sdk_version"
    implementation "org.apache.kafka:kafka-clients:$kafka_version"
    implementation "org.apache.arrow:arrow-vector:$arrow_version"
    compileOnly "com.oracle.substratevm:svm:$graalvm_version"

    testImplementation "org.spekframework.spek2:spek-dsl-jvm:$spek_version"
    testRuntimeOnly "org.spekframework.spek2:spek-runner-junit5:$spek_version"
    testImplementation "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
    testImplementation "org.hamcrest:hamcrest-all:$hamcrest_version"
    testImplementation "org.junit.platform:junit-platform-runner:$junit_platform_runner_version"
}

compileKotlin {
    dependsOn generateGrammarSource
    kotlinOptions.jvmTarget = "1.8"
}

compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

test {
    useJUnitPlatform {
        includeEngines 'spek2'
    }
}

jar {
    manifest {
        attributes 'Main-Class': 'jsonsql.shell.ShellKt'
    }
}

shadowJar {
    archiveClassifier = null
    archiveVersion = ''
}
